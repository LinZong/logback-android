apply plugin: 'com.android.library'
apply from: 'maven-push.gradle'

android {
    compileSdkVersion 15

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_5
        targetCompatibility JavaVersion.VERSION_1_5
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 15
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkAllWarnings true
        lintConfig rootProject.file('gradle/lint.xml')
    }
}

dependencies {
    testImplementation 'org.mockito:mockito-core:2.13.0'
    testImplementation(group: 'junit', name: 'junit', version: '4.12') {
        exclude(module: 'hamcrest-core')
    }
    testImplementation 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testImplementation 'org.robolectric:robolectric:3.6.1'

    // This dep is required when using Robolectric and targeting pre-21 SDKs.
    // Otherwise, tests fail with java.lang.NoClassDefFoundError.
    // https://github.com/robolectric/robolectric/issues/2562#issuecomment-263086503
    testImplementation 'org.robolectric:android-all:8.1.0-robolectric-r4458339'

    implementation 'com.github.tony19:apktool-lib:1.4.4-3'

    testImplementation 'com.icegreen:greenmail:1.5.6'
    testImplementation 'dom4j:dom4j:1.6.1'
    testImplementation 'org.easytesting:fest-assert:1.4'
    testImplementation "org.slf4j:integration:${slf4jVersion}"
    testImplementation "org.slf4j:log4j-over-slf4j:${slf4jVersion}"
    testImplementation "org.slf4j:slf4j-api:${slf4jVersion}:tests"
    testImplementation "org.slf4j:slf4j-ext:${slf4jVersion}"
    testImplementation(group: 'org.subethamail', name: 'subethasmtp', version: '2.1.0') {
        exclude(module: 'slf4j-api')
    }
    compileOnly 'com.google.android:android:2.1.2'
    compileOnly "org.slf4j:slf4j-api:${slf4jVersion}"

    // For SMTPAppender
    compileOnly 'com.sun.mail:android-mail:1.6.0'
    compileOnly 'com.sun.mail:android-activation:1.6.0'

    testAnnotationProcessor 'ch.qos.cal10n:cal10n-api:0.8.1'
}

configurations {
    all {
        exclude module: 'httpclient'
        exclude module: 'commons-logging'
        exclude module: 'opengl-api'
        exclude module: 'xmlParserAPIs'
        exclude module: 'xpp3'
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        // Warn about deprecations
        options.compilerArgs << '-Xlint:deprecation'
        // Warn about unchecked usages
        options.compilerArgs << '-Xlint:unchecked'
        // Don't warn about using source/target 1.5 option
        options.compilerArgs << '-Xlint:-options'
    }
}
