apply plugin: 'org.ajoberstar.grgit'

/**
 * Creates combined javadocs for all subprojects
 */
task javadocs(type: Javadoc) {
  source subprojects.collect { project -> project.sourceSets.main.allJava }
  classpath = files(subprojects.collect { project -> project.sourceSets.main.compileClasspath })
  failOnError = false
}

/**
 * Uploads javadocs to gh-pages
 */
task uploadDocs(dependsOn: javadocs) << {
  def origBranch = grgit.branch.current.name
  grgit.checkout(branch: 'gh-pages', createBranch: false)
  def dest = "doc/${rootProject.version}"
  FileUtils.copyDirectory(new File("build/docs/javadoc"), new File(dest))
  grgit.add(patterns: [dest])
  grgit.commit(message: ":books: Add javadoc for ${rootProject.version}")
  grgit.push()
  grgit.checkout(branch: origBranch)
}
